# Distributed under the Boost Software License, Version 1.0.
# See http://www.boost.org/LICENSE_1_0.txt

cmake_minimum_required(VERSION 2.8)
project(BoostMath CXX)

find_package(Boost
  COMPONENTS
    algorithm
    array
    bind
    cmake
    concept_check
    config
    conversion
    core
    exception
    format
    function
    fusion
    integer
    mpl
    numeric
    random
    regex
    smart_ptr
    spirit
    static_assert
    test
    timer
    tr1
    tuple
    type_traits
    utility
  NO_MODULE
  )
include(${Boost_USE_FILE})
include(BoostAddLibrary)
include(BoostExport)

include_directories(
  include
  src/tr1
  )

set(C99_SOURCES
  acosh
  asinh
  atanh
  cbrt
  copysign
  erfc
  erf
  expm1
  fmax
  fmin
  fpclassify
  hypot
  lgamma
  llround
  log1p
  lround
  nextafter
  nexttoward
  round
  tgamma
  trunc
  )

set(TR1_SOURCES
  assoc_laguerre
  assoc_legendre
  beta
  comp_ellint_1
  comp_ellint_2
  comp_ellint_3
  cyl_bessel_i
  cyl_bessel_j
  cyl_bessel_k
  cyl_neumann
  ellint_1
  ellint_2
  ellint_3
  expint
  hermite
  laguerre
  legendre
  riemann_zeta
  sph_bessel
  sph_legendre
  sph_neumann
  )

function(add_math_library name suffix)
  set(sources)
  foreach(src ${ARGN})
    list(APPEND sources "src/tr1/${src}${suffix}.cpp")
  endforeach(src)
  boost_add_library(math_${name}
    PRECOMPILE <boost/math/special_functions.hpp>
    SOURCES ${sources}
    )
endfunction(add_math_library)

add_math_library(tr1  ""  ${TR1_SOURCES})
add_math_library(tr1f "f" ${TR1_SOURCES})
add_math_library(tr1l "l" ${TR1_SOURCES})
add_math_library(c99  ""  ${C99_SOURCES})
add_math_library(c99f "f" ${C99_SOURCES})
add_math_library(c99l "l" ${C99_SOURCES})

boost_export(
  TARGETS
    math_tr1
    math_tr1f
    math_tr1l
    math_c99
    math_c99f
    math_c99l
  BOOST_DEPENDS
    algorithm
    array
    bind
    concept_check
    config
    conversion
    core
    exception
    format
    function
    fusion
    integer
    mpl
    numeric
    smart_ptr
    spirit
    static_assert
    tr1
    tuple
    type_traits
    utility
  INCLUDE_DIRECTORIES
    include
  )
